use crate::ast;
use crate::ast::UnwrapWithVec;
use crate::without_none;

grammar<'a>(errors: &mut Vec<ast::PtxError>);

match {
    r"\s+" => { },
    r"//[^\n\r]*[\n\r]*" => { },
    r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { },
    r"-?[?:0x]?[0-9]+" => Num,
    r#""[^"]*""# => String,
    r"[0-9]+\.[0-9]+" => VersionNumber,
    "!",
    "(", ")",
    "+",
    ",",
    ".",
    ":",
    ";",
    "@",
    "[", "]",
    "{", "}",
    "|",
    ".acquire",
    ".address_size",
    ".and",
    ".b16",
    ".b32",
    ".b64",
    ".b8",
    ".ca",
    ".cg",
    ".const",
    ".cs",
    ".cta",
    ".cv",
    ".entry",
    ".eq",
    ".equ",
    ".extern",
    ".f16",
    ".f16x2",
    ".f32",
    ".f64",
    ".file",
    ".ftz",
    ".func",
    ".ge",
    ".geu",
    ".global",
    ".gpu",
    ".gt",
    ".gtu",
    ".hi",
    ".hs",
    ".le",
    ".leu",
    ".lo",
    ".loc",
    ".local",
    ".ls",
    ".lt",
    ".ltu",
    ".lu",
    ".nan",
    ".ne",
    ".neu",
    ".num",
    ".or",
    ".param",
    ".pred",
    ".reg",
    ".relaxed",
    ".rm",
    ".rmi",
    ".rn",
    ".rni",
    ".rp",
    ".rpi",
    ".rz",
    ".rzi",
    ".s16",
    ".s32",
    ".s64",
    ".s8" ,
    ".sat",
    ".section",
    ".shared",
    ".sreg",
    ".sys",
    ".target",
    ".u16",
    ".u32",
    ".u64",
    ".u8" ,
    ".uni",
    ".v2",
    ".v4",
    ".version",
    ".visible",
    ".volatile",
    ".wb",
    ".weak",
    ".wide",
    ".wt",
    ".xor",
} else {
    // IF YOU ARE ADDING A NEW TOKEN HERE ALSO ADD IT BELOW TO ExtendedID
    "add",
    "bra",
    "cvt",
    "debug",
    "ld",
    "map_f64_to_f32",
    "mov",
    "mul",
    "not",
    "ret",
    "setp",
    "shl",
    "shr",
    r"sm_[0-9]+" => ShaderModel,
    "st",
    "texmode_independent",
    "texmode_unified",
} else {
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#identifiers
    r"[a-zA-Z][a-zA-Z0-9_$]*|[_$%][a-zA-Z0-9_$]+" => ID,
    r"\.[a-zA-Z][a-zA-Z0-9_$]*" => DotID,
} else {
    r"(?:[a-zA-Z][a-zA-Z0-9_$]*|[_$%][a-zA-Z0-9_$]+)<[0-9]+>" => ParametrizedID,
}

ExtendedID : &'input str = {
    "add",
    "bra",
    "cvt",
    "debug",
    "ld",
    "map_f64_to_f32",
    "mov",
    "mul",
    "not",
    "ret",
    "setp",
    "shl",
    "shr",
    ShaderModel,
    "st",
    "texmode_independent",
    "texmode_unified",
    ID
}

pub Module: ast::Module<'input> = {
    <v:Version> Target <f:Directive*> => {
        ast::Module { version: v, functions: without_none(f) }
    }
};

Version: (u8, u8) = {
    ".version" <v:VersionNumber> => {
        let dot = v.find('.').unwrap();
        let major = v[..dot].parse::<u8>();
        let minor = v[dot+1..].parse::<u8>();
        (major,minor).unwrap_with(errors)
    }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#ptx-module-directives-target
Target = {
    ".target" Comma<TargetSpecifier>
};

TargetSpecifier = {
    ShaderModel,
    "texmode_unified",
    "texmode_independent",
    "debug",
    "map_f64_to_f32"
};

Directive: Option<ast::Function<'input>> = {
    AddressSize => None,
    <f:Function> => Some(f),
    File => None,
    Section => None
};

AddressSize = {
    ".address_size" Num
};

Function: ast::Function<'input>  = {
    LinkingDirective*
    <kernel:IsKernel>
    <name:ExtendedID>
    "(" <args:Comma<FunctionInput>> ")"
    <body:FunctionBody> => ast::Function{<>}
};

LinkingDirective = {
    ".extern",
    ".visible",
    ".weak"
};

IsKernel: bool = {
    ".entry" => true,
    ".func" => false
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parameter-state-space
FunctionInput: ast::Argument<'input> = {
    ".param" <_type:ScalarType> <name:ExtendedID> => { 
        ast::Argument {a_type: _type, name: name, length: 1 }
    },
    ".param" <a_type:ScalarType> <name:ExtendedID> "[" <length:Num> "]" => {
        let length = length.parse::<u32>();
        let length = length.unwrap_with(errors);
        ast::Argument { a_type: a_type, name: name, length: length }
    }
};

pub(crate) FunctionBody: Vec<ast::Statement<&'input str>> = {
    "{" <s:Statement*> "}" => { without_none(s) }
};

StateSpaceSpecifier: ast::StateSpace = {
    ".reg" => ast::StateSpace::Reg,
    ".sreg" => ast::StateSpace::Sreg,
    ".const" => ast::StateSpace::Const,
    ".global" => ast::StateSpace::Global,
    ".local" => ast::StateSpace::Local,
    ".shared" => ast::StateSpace::Shared
};


Type: ast::Type = {
    <t:ScalarType> => ast::Type::Scalar(t),
    <t:ExtendedScalarType> => ast::Type::ExtendedScalar(t),
};

ScalarType: ast::ScalarType = {
    ".f16" => ast::ScalarType::F16,
    MemoryType
};

ExtendedScalarType: ast::ExtendedScalarType = {
    ".f16x2" => ast::ExtendedScalarType::F16x2,
    ".pred" => ast::ExtendedScalarType::Pred,
};

MemoryType: ast::ScalarType = {
    ".b8" => ast::ScalarType::B8,
    ".b16" => ast::ScalarType::B16,
    ".b32" => ast::ScalarType::B32,
    ".b64" => ast::ScalarType::B64,
    ".u8" => ast::ScalarType::U8,
    ".u16" => ast::ScalarType::U16,
    ".u32" => ast::ScalarType::U32,
    ".u64" => ast::ScalarType::U64,
    ".s8" => ast::ScalarType::S8,
    ".s16" => ast::ScalarType::S16,
    ".s32" => ast::ScalarType::S32,
    ".s64" => ast::ScalarType::S64,
    ".f32" => ast::ScalarType::F32,
    ".f64" => ast::ScalarType::F64,
};

Statement: Option<ast::Statement<&'input str>> = {
    <l:Label> => Some(ast::Statement::Label(l)),
    DebugDirective => None,
    <v:Variable> ";" => Some(ast::Statement::Variable(v)),
    <p:PredAt?> <i:Instruction> ";" => Some(ast::Statement::Instruction(p, i))
};

DebugDirective: () = {
    DebugLocation
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#debugging-directives-loc
DebugLocation = {
    ".loc" Num Num Num
};

Label: &'input str = {
    <id:ExtendedID> ":" => id
};

Variable: ast::Variable<&'input str> = {
    <s:StateSpaceSpecifier> <t:Type> <v:VariableName> => {
        let (name, count) = v;
        ast::Variable { space: s, v_type: t, name: name, count: count }
    }
};

VariableName: (&'input str, Option<u32>) = {
    <id:ExtendedID> => (id, None),
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parameterized-variable-names
    <id:ParametrizedID> => {
        let left_angle = id.as_bytes().iter().copied().position(|x| x == b'<').unwrap();
        let count = id[left_angle+1..id.len()-1].parse::<u32>();
        let count = match count {
            Ok(c) => Some(c),
            Err(e) => { errors.push(e.into()); None },
        };
        (&id[0..left_angle], count)
    }
};

Instruction: ast::Instruction<&'input str> = {
    InstLd,
    InstMov,
    InstMul,
    InstAdd,
    InstSetp,
    InstNot,
    InstBra,
    InstCvt,
    InstShl,
    InstSt,
    InstRet,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-ld
InstLd: ast::Instruction<&'input str> = {
    "ld" <q:LdStQualifier?> <ss:LdStateSpace?> <cop:LdCacheOperator?> <v:VectorPrefix?> <t:MemoryType> <dst:ExtendedID> "," "[" <src:Operand> "]" => {
        ast::Instruction::Ld(
            ast::LdData {
                qualifier: q.unwrap_or(ast::LdStQualifier::Weak),
                state_space: ss.unwrap_or(ast::LdStateSpace::Generic),
                caching: cop.unwrap_or(ast::LdCacheOperator::Cached),
                vector: v,
                typ: t
            },
            ast::Arg2 { dst:dst, src:src }
        )
    }
};

LdStQualifier: ast::LdStQualifier = {
    ".weak" => ast::LdStQualifier::Weak,
    ".volatile" => ast::LdStQualifier::Volatile,
    ".relaxed" <s:LdScope> => ast::LdStQualifier::Relaxed(s),
    ".acquire" <s:LdScope> => ast::LdStQualifier::Acquire(s),
};

LdScope: ast::LdScope = {
    ".cta" => ast::LdScope::Cta,
    ".gpu" => ast::LdScope::Gpu,
    ".sys" => ast::LdScope::Sys
};

LdStateSpace: ast::LdStateSpace = {
    ".const" => ast::LdStateSpace::Const,
    ".global" => ast::LdStateSpace::Global,
    ".local" => ast::LdStateSpace::Local,
    ".param" => ast::LdStateSpace::Param,
    ".shared" => ast::LdStateSpace::Shared,
};

LdCacheOperator: ast::LdCacheOperator = {
    ".ca" => ast::LdCacheOperator::Cached,
    ".cg" => ast::LdCacheOperator::L2Only,
    ".cs" => ast::LdCacheOperator::Streaming,
    ".lu" => ast::LdCacheOperator::LastUse,
    ".cv" => ast::LdCacheOperator::Uncached,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-mov
InstMov: ast::Instruction<&'input str> = {
    "mov" <t:MovType> <a:Arg2Mov> => {
        ast::Instruction::Mov(ast::MovData{ typ:t }, a)
    }
};

MovType: ast::Type = {
    ".b16" => ast::Type::Scalar(ast::ScalarType::B16),
    ".b32" => ast::Type::Scalar(ast::ScalarType::B32),
    ".b64" => ast::Type::Scalar(ast::ScalarType::B64),
    ".u16" => ast::Type::Scalar(ast::ScalarType::U16),
    ".u32" => ast::Type::Scalar(ast::ScalarType::U32),
    ".u64" => ast::Type::Scalar(ast::ScalarType::U64),
    ".s16" => ast::Type::Scalar(ast::ScalarType::S16),
    ".s32" => ast::Type::Scalar(ast::ScalarType::S32),
    ".s64" => ast::Type::Scalar(ast::ScalarType::S64),
    ".f32" => ast::Type::Scalar(ast::ScalarType::F32),
    ".f64" => ast::Type::Scalar(ast::ScalarType::F64),
    ".pred" => ast::Type::ExtendedScalar(ast::ExtendedScalarType::Pred)
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-mul
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-mul
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-mul
InstMul: ast::Instruction<&'input str> = {
    "mul" <d:InstMulMode> <a:Arg3> => ast::Instruction::Mul(d, a)
};

InstMulMode: ast::MulData = {
    MulIntControl? IntType => ast::MulData{},
    RoundingMode? ".ftz"? ".sat"? ".f32" => ast::MulData{},
    RoundingMode? ".f64" => ast::MulData{},
    ".rn"? ".ftz"? ".sat"? ".f16" => ast::MulData{},
    ".rn"? ".ftz"? ".sat"? ".f16x2" => ast::MulData{}
};

MulIntControl = {
    ".hi", ".lo", ".wide"
};

#[inline]
RoundingMode = {
    ".rn", ".rz", ".rm", ".rp"
};

IntType : ast::ScalarType = {
    ".u16" => ast::ScalarType::U16,
    ".u32" => ast::ScalarType::U32,
    ".u64" => ast::ScalarType::U64,
    ".s16" => ast::ScalarType::S16,
    ".s32" => ast::ScalarType::S32,
    ".s64" => ast::ScalarType::S64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-add
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-add
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-add
InstAdd: ast::Instruction<&'input str> = {
    "add" <d:InstAddMode> <a:Arg3> => ast::Instruction::Add(d, a)
};

InstAddMode: ast::AddData = {
    <t:IntType> => ast::AddData{ typ: t },
    ".sat" ".s32" => ast::AddData{ typ: ast::ScalarType::S32 },
    RoundingMode? ".ftz"? ".sat"? ".f32" => ast::AddData{ typ: ast::ScalarType::F32 },
    RoundingMode? ".f64" => ast::AddData{ typ: ast::ScalarType::F64 },
    ".rn"? ".ftz"? ".sat"? ".f16" => ast::AddData{ typ: ast::ScalarType::F16 },
    ".rn"? ".ftz"? ".sat"? ".f16x2" => todo!()
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#comparison-and-selection-instructions-setp
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-comparison-instructions-setp
// TODO: support f16 setp
InstSetp: ast::Instruction<&'input str> = {
    "setp" <d:SetpMode> <a:Arg4> => ast::Instruction::Setp(d, a),
    "setp" <d:SetpBoolMode> <a:Arg5> => ast::Instruction::SetpBool(d, a),
};

SetpMode: ast::SetpData = {
    SetpCmpOp ".ftz"? SetpType => ast::SetpData{}
};

SetpBoolMode: ast::SetpBoolData = {
    SetpCmpOp SetpBoolOp ".ftz"? SetpType => ast::SetpBoolData{}
};

SetpCmpOp = {
    ".eq", ".ne", ".lt", ".le", ".gt", ".ge", ".lo", ".ls", ".hi", ".hs",
    ".equ", ".neu", ".ltu", ".leu", ".gtu", ".geu", ".num", ".nan"
};

SetpBoolOp = {
    ".and", ".or", ".xor"
};

SetpType = {
    ".b16", ".b32", ".b64",
    ".u16", ".u32", ".u64",
    ".s16", ".s32", ".s64",
    ".f32", ".f64"
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-not
InstNot: ast::Instruction<&'input str> = {
    "not" NotType <a:Arg2> => ast::Instruction::Not(ast::NotData{}, a)
};

NotType = {
    ".pred", ".b16", ".b32", ".b64"
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-at
PredAt: ast::PredAt<&'input str> = {
    "@" <label:ExtendedID> => ast::PredAt { not: false, label:label },
    "@" "!" <label:ExtendedID> => ast::PredAt { not: true, label:label }
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-bra
InstBra: ast::Instruction<&'input str> = {
    "bra" <u:".uni"?> <a:Arg1> => ast::Instruction::Bra(ast::BraData{ uniform: u.is_some() }, a)
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cvt
InstCvt: ast::Instruction<&'input str> = {
    "cvt" CvtRnd? ".ftz"? ".sat"? CvtType CvtType <a:Arg2> => {
        ast::Instruction::Cvt(ast::CvtData{}, a)
    }
};

CvtRnd = {
    CvtIrnd,
    CvtFrnd
}

CvtIrnd = {
    ".rni", ".rzi", ".rmi", ".rpi"
};

CvtFrnd = {
    ".rn",  ".rz",  ".rm", ".rp"
};

CvtType = {
    ".u8", ".u16", ".u32", ".u64",
    ".s8", ".s16", ".s32", ".s64",
    ".f16", ".f32", ".f64"
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-shl
InstShl: ast::Instruction<&'input str> = {
    "shl" ShlType <a:Arg3> => ast::Instruction::Shl(ast::ShlData{}, a)
};

ShlType = {
    ".b16", ".b32", ".b64"
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-st
// Warning: NVIDIA documentation is incorrect, you can specify scope only once
InstSt: ast::Instruction<&'input str> = {
    "st" <q:LdStQualifier?> <ss:StStateSpace?> <cop:StCacheOperator?> <v:VectorPrefix?> <t:MemoryType> "[" <src1:Operand> "]" "," <src2:Operand> => {
        ast::Instruction::St(
            ast::StData {
                qualifier: q.unwrap_or(ast::LdStQualifier::Weak),
                state_space: ss.unwrap_or(ast::StStateSpace::Generic),
                caching: cop.unwrap_or(ast::StCacheOperator::Writeback),
                vector: v,
                typ: t
            },
            ast::Arg2St { src1:src1, src2:src2 }
        )
    }
};

StStateSpace: ast::StStateSpace = {
    ".global" => ast::StStateSpace::Global,
    ".local" => ast::StStateSpace::Local,
    ".param" => ast::StStateSpace::Param,
    ".shared" => ast::StStateSpace::Shared,
};

StCacheOperator: ast::StCacheOperator = {
    ".wb" => ast::StCacheOperator::Writeback,
    ".cg" => ast::StCacheOperator::L2Only,
    ".cs" => ast::StCacheOperator::Streaming,
    ".wt" => ast::StCacheOperator::Writethrough,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-ret
InstRet: ast::Instruction<&'input str> = {
    "ret" <u:".uni"?> => ast::Instruction::Ret(ast::RetData { uniform: u.is_some() })
};

Operand: ast::Operand<&'input str> = {
    <r:ExtendedID> => ast::Operand::Reg(r),
    <r:ExtendedID> "+" <o:Num> => {
        let offset = o.parse::<i32>();
        let offset = offset.unwrap_with(errors);
        ast::Operand::RegOffset(r, offset)
    },
    // TODO: start parsing whole constants sub-language:
    //       https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#constants
    <o:Num> => {
        let offset = o.parse::<i128>();
        let offset = offset.unwrap_with(errors);
        ast::Operand::Imm(offset)
    }
};

MovOperand: ast::MovOperand<&'input str> = {
    <o:Operand> => ast::MovOperand::Op(o),
    <o:VectorOperand> => {
        let (pref, suf) = o;
        ast::MovOperand::Vec(pref.to_string(), suf.to_string())
    }
};

VectorOperand: (&'input str, &'input str) = {
    <pref:ExtendedID> "." <suf:ExtendedID> => (pref, suf),
    <pref:ExtendedID> <suf:DotID> => (pref, &suf[1..]),
};

Arg1: ast::Arg1<&'input str> = {
    <src:ExtendedID> => ast::Arg1{<>}
};

Arg2: ast::Arg2<&'input str> = {
    <dst:ExtendedID> "," <src:Operand> => ast::Arg2{<>}
};

Arg2Mov: ast::Arg2Mov<&'input str> = {
    <dst:ExtendedID> "," <src:MovOperand> => ast::Arg2Mov{<>}
};

Arg3: ast::Arg3<&'input str> = {
    <dst:ExtendedID> "," <src1:Operand> "," <src2:Operand> => ast::Arg3{<>}
};

Arg4: ast::Arg4<&'input str> = {
    <dst1:ExtendedID> <dst2:OptionalDst?> "," <src1:Operand> "," <src2:Operand> => ast::Arg4{<>}
};

// TODO: pass src3 negation somewhere
Arg5: ast::Arg5<&'input str> = {
    <dst1:ExtendedID> <dst2:OptionalDst?> "," <src1:Operand> "," <src2:Operand> "," "!"? <src3:Operand> => ast::Arg5{<>}
};

OptionalDst: &'input str = {
    "|" <dst2:ExtendedID> => dst2
}

VectorPrefix: ast::VectorPrefix = {
    ".v2" => ast::VectorPrefix::V2,
    ".v4" => ast::VectorPrefix::V4
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#debugging-directives-file
File = {
    ".file" Num String ("," Num "," Num)?
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#debugging-directives-section
Section = {
    ".section" DotID "{" SectionDwarfLines* "}"
};

SectionDwarfLines: () = {
    BitType Comma<Num>,
    ".b32" SectionLabel,
    ".b64" SectionLabel,
    ".b32" SectionLabel "+" Num,
    ".b64" SectionLabel "+" Num,
};

SectionLabel = {
    ID,
    DotID
};

BitType = {
    ".b8", ".b16", ".b32", ".b64"
};

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};
